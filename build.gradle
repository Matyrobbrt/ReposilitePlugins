import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import java.nio.file.Files
import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import java.util.zip.ZipInputStream
import java.util.zip.ZipOutputStream

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

archivesBaseName = 'reposiliteplugins'
group = 'com.matyrobbrt.reposiliteplugins'

def plugins = [
        'updateJson', 'webhooks', 'badges',
        'uncompressed', 'pluginInstaller'
]

sourceSets {
    all
}

sourceSets {
    restart
}

plugins.each {
    final srcSet = sourceSets.create(it)
    configurations.getByName(srcSet.compileOnlyConfigurationName).dependencies.add(project.dependencies.create(sourceSets.main.output))
    configurations.getByName(srcSet.compileClasspathConfigurationName).extendsFrom(configurations.compileOnly)
    configurations.allImplementation.dependencies.add(project.dependencies.create(srcSet.output))
    tasks.shadowJar.from(srcSet.output)
}

application {
    mainClass.set('com.reposilite.ReposiliteLauncherKt')
}
tasks.named('run', JavaExec).configure {
    it.setWorkingDir(project.file('run'))
    it.dependsOn(shadowJar)
    it.doFirst {
        final outDir = project.file('run/plugins').toPath()
        Files.createDirectories(outDir)
        final out = outDir.resolve("reposiliteplugins-${version}.jar")
        Files.deleteIfExists(out)
        Files.copy(shadowJar.archiveFile.get().asFile.toPath(), out)
    }
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.reposilite.com/releases'
    }
    maven {
        url = 'https://maven.reposilite.com/snapshots'
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)

    plugins.each {
        getByName("${it}Implementation").extendsFrom(shade)
    }
}

dependencies {
    compileOnly('com.reposilite:reposilite:3.2.5:all')
    runtimeOnly('com.reposilite:reposilite:3.2.5:all') {
        exclude module: 'reposilite-frontend'
    }

    shade 'com.google.code.gson:gson:2.9.1'
    shade('club.minnced:discord-webhooks:0.8.2') {
        exclude group: 'org.jetbrains.kotlin'
    }
}

tasks.register('relocateShadowJar', ConfigureShadowRelocation).configure {
    it.target = tasks.shadowJar
    it.prefix = 'com.matyrobbrt.reposiliteplugins.shade'
}

tasks.register('restartJar', Jar).configure {
    it.from(sourceSets.restart.output)
    it.manifest.attributes(['Main-Class': 'com.matyrobbrt.reposiliteplugins.restart.Main'])
    it.archiveFileName = 'restarter.jar'
}

shadowJar {
    from sourceSets.all.output
    configurations = [project.configurations.shade]

    dependsOn(tasks.relocateShadowJar, tasks.restartJar)

    doLast {
        final copyPath = file("$buildDir/libs/old.jar").toPath()
        Files.deleteIfExists(copyPath)
        Files.copy(archiveFile.get().asFile.toPath(), copyPath)
        try (final is = new ZipInputStream(Files.newInputStream(copyPath));
            final zis = new ZipOutputStream(new FileOutputStream(archiveFile.get().asFile))) {
            zis.putNextEntry(new ZipEntry('restarter.jar'))
            zis.write(((File)tasks.restartJar.archiveFile.get().asFile).readBytes())
            zis.closeEntry()

            ZipEntry entry
            while ((entry = is.nextEntry) !== null) {
                zis.putNextEntry(entry)
                zis.write(is.readAllBytes())
                zis.closeEntry()
            }
        }
    }
}